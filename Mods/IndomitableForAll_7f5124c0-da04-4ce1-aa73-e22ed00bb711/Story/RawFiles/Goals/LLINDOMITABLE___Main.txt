Version 1
SubGoalCombiner SGC_AND
INITSECTION
NRD_ModCall("IndomitableForAll", "StoreModVersion");
KBSECTION
IF
TextEventSet("llindom_luareset")
THEN
NRD_LuaReset(1);

IF
TextEventSet("llindom_settings")
AND
CharacterGetHostCharacter(_Host)
THEN
Proc_StartDialog(0, "LLINDOMITABLE_SettingsMenu", _Host);

//REGION REMOVE_FROM_ALL
PROC
LLINDOMITABLE_RemoveFromAll()
THEN
CharacterLaunchOsirisOnlyIterator("LLINDOMITABLE_RemoveTalent");

IF
StoryEvent((CHARACTERGUID)_Char, "LLINDOMITABLE_RemoveTalent")
AND
CharacterIsPlayer(_Char, _IsPlayer)
THEN
LLINDOMITABLE_RemoveTalent(_Char, _IsPlayer);

PROC
LLINDOMITABLE_RemoveTalent((CHARACTERGUID)_Char, 1) // IsPlayer
AND
CharacterHasTalent(_Char, "Indomitable", 1)
THEN
CharacterRemoveTalent(_Char, "Indomitable");
NRD_PlayerSetBaseTalent(_Char, "Indomitable", 0);
CharacterAddCivilAbilityPoint(_Char, 0);

PROC
LLINDOMITABLE_RemoveTalent((CHARACTERGUID)_Char, 0) // NPC
THEN
NRD_ModCall("IndomitableForAll", "RemoveTalent", (STRING)_Char, "Indomitable");
//END_REGION

PROC
LLINDOMITABLE_ApplyIndomitable((CHARACTERGUID)_Char)
AND
GlobalGetFlag("LLINDOMITABLE_Settings_ApplyOnAttempt", _OnAttempt)
THEN
LLINDOMITABLE_ApplyIndomitable(_Char, _OnAttempt);

PROC
LLINDOMITABLE_ApplyIndomitable((CHARACTERGUID)_Char, 0)
AND
DB_LLINDOMITABLE_ExtraData_Real("LLINDOMITABLE_IndomitableDuration_OnRemoval", _Duration, _, _)
THEN
ApplyStatus(_Char, "LLINDOMITABLE_INDOMITABLE", _Duration, 0, _Char);

PROC
LLINDOMITABLE_ApplyIndomitable((CHARACTERGUID)_Char, 1)
AND
DB_LLINDOMITABLE_ExtraData_Real("LLINDOMITABLE_IndomitableDuration_OnAttempt", _Duration, _, _)
THEN
ApplyStatus(_Char, "LLINDOMITABLE_INDOMITABLE", _Duration, 0, _Char);

//REGION LEADERLIB
IF
ObjectFlagSet("LLINDOMITABLE_SaveGlobalSettings", (CHARACTERGUID)_Char, _)
THEN
ObjectClearFlag(_Char, "LLINDOMITABLE_SaveGlobalSettings", 0);
LLINDOMITABLE_SaveGlobalSettings(_Char);

PROC
LLINDOMITABLE_SaveGlobalSettings()
AND
CharacterGetHostCharacter(_Host)
THEN
LLINDOMITABLE_SaveGlobalSettings(_Host);

PROC
LLINDOMITABLE_SaveGlobalSettings((CHARACTERGUID)_Char)
AND
GlobalGetFlag("LeaderLib_Initialized", 1)
AND
GlobalGetFlag("LeaderLib_ExtenderIsActive", 1)
AND
CharacterGetHostCharacter(_Host)
THEN
SetStoryEvent(_Host, "LeaderLib_Commands_SaveGlobalSettings");

PROC
LLINDOMITABLE_InitLeaderLibSettings()
AND
// DB_LeaderLib_ModMenu_RegisteredMenuData(_ID, _DisplayName, _Dialog, _ModID, _Author);
NOT DB_LeaderLib_ModMenu_RegisteredMenuData("7f5124c0-da04-4ce1-aa73-e22ed00bb711", (STRING)_, "LLINDOMITABLE_SettingsMenu", (STRING)_, (STRING)_)
THEN
DB_LeaderLib_ModApi_RegisterMenu("7f5124c0-da04-4ce1-aa73-e22ed00bb711", "LLINDOMITABLE_SettingsMenu", "[Indomitable For All] Settings");

/* Called when a mod is registered for the first time. */
/*
PROC
LeaderLib_Mods_OnModRegistered(7f5124c0-da04-4ce1-aa73-e22ed00bb711, (INTEGER)_Version, (INTEGER)_Major, (INTEGER)_Minor, (INTEGER)_Revision, (INTEGER)_Build)
THEN
LLINDOMITABLE_InitLeaderLibSettings();
*/

/* Called when a mod's registered version changes. */
PROC
LeaderLib_Mods_OnVersionChanged(7f5124c0-da04-4ce1-aa73-e22ed00bb711, (INTEGER)_LastVersion, (INTEGER)_NextVersion)
AND
NOT DB_LLINDOMITABLE_RegisteredLeaderLibSettings(1)
THEN
DB_LLINDOMITABLE_RegisteredLeaderLibSettings(1);
LLINDOMITABLE_InitLeaderLibSettings();

/*
PROC
LeaderLib_Mods_OnVersionChanged(7f5124c0-da04-4ce1-aa73-e22ed00bb711, (INTEGER)_LastMajor, (INTEGER)_LastMinor, (INTEGER)_LastRevision, (INTEGER)_LastBuild, (INTEGER)_NextMajor, (INTEGER)_NextMinor, (INTEGER)_NextRevision, (INTEGER)_NextBuild)
THEN
DB_NOOP(1);
*/
//END_REGION

//REGION UPDATING
IF
SavegameLoading(_,_,_,_)
THEN
NRD_ModCall("IndomitableForAll", "UpdateMod");

PROC
LLINDOMITABLE_OnGetVersion((INTEGER)_Next)
AND
DB_LLINDOMITABLE_LastVersion(_Last)
THEN
NOT DB_LLINDOMITABLE_LastVersion(_Last);
DB_LLINDOMITABLE_LastVersion(_Next);
LLINDOMITABLE_OnVersionChanged(_Last, _Next);

PROC
LLINDOMITABLE_OnGetVersion((INTEGER)_Next)
AND
NOT DB_LLINDOMITABLE_LastVersion(_)
THEN
DB_LLINDOMITABLE_LastVersion(_Next);
LLINDOMITABLE_OnVersionChanged(-1, _Next);

PROC
LLINDOMITABLE_OnVersionChanged((INTEGER)_Last, (INTEGER)_Next)
THEN
DB_NOOP(1);

// Remove all Indomitable talents
PROC
LLINDOMITABLE_OnVersionChanged(-1, (INTEGER)_Next)
AND
_Next > 268435456
AND
NOT SysIsActive("LLINDOMITABLE_Updater_GameStarted")
THEN
SysActivateGoal("LLINDOMITABLE_Updater_GameStarted");
//END_REGION
EXITSECTION
ENDEXITSECTION
ParentTargetEdge "LaughingLeader_IndomitableForAll"